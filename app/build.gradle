apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    // get the first 8 chars of the latest git commit, and use it as the version build name
    def appBuildPrefix =  ['sh', '-c', 'git rev-parse --short=8 HEAD'].execute().text.trim()

    defaultConfig {
        applicationId "name.lmj0011.redditdraftking"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "0.1.0"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // ref: https://stackoverflow.com/a/48674264/2445763
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            resValue("string", "app_build", appBuildPrefix)
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            resValue("string", "app_build", appBuildPrefix)
            applicationIdSuffix '.debug'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    dataBinding {
        enabled = true
    }

    // Big Up! to cSn: https://stackoverflow.com/a/36626584/2445763
    task generateDependencyListFile {
        configurations.implementation.setCanBeResolved(true)
        configurations.androidTestImplementation.setCanBeResolved(true)

        doLast {
            def str = "# auto-generated from ${this.name}; this file should be checked into version control\n"
            def resolvedImplementationConfig = configurations.implementation.resolvedConfiguration
            def resolvedAndroidTestImplementationConfig = configurations.androidTestImplementation.resolvedConfiguration

            resolvedImplementationConfig.firstLevelModuleDependencies.each { dep ->
                str += "${dep.moduleGroup}:${dep.moduleName}:${dep.moduleVersion}\n"
            }

            resolvedAndroidTestImplementationConfig.firstLevelModuleDependencies.each { dep ->
                str += "${dep.moduleGroup}:${dep.moduleName}:${dep.moduleVersion}\n"
            }

            new File(projectDir, "deps.list.txt").text = str
        }
    }

    preBuild.dependsOn(generateDependencyListFile)
    /////
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0-alpha02'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.navigation:navigation-fragment:2.3.0'
    implementation 'androidx.navigation:navigation-ui:2.3.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Room dependencies
    final room_version = '2.2.5'
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // WorkManager
    def work_version = "2.4.0"
    implementation "androidx.work:work-runtime-ktx:$work_version"

    final coroutines_version = '1.3.9'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // androidx.preference
    implementation "androidx.preference:preference-ktx:1.1.1"

    // Reddit oauth
    implementation 'com.github.KirkBushman:Android-Reddit-OAuth2:1.2.2'

    // dependency injection
    implementation 'org.kodein.di:kodein-di:7.0.0'

    // An adaptation of the JSR-310 backport for Android.
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.squareup.okhttp3:okhttp:4.8.1'
    implementation 'com.squareup.moshi:moshi-kotlin:1.9.3'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    // https://stackoverflow.com/a/38922755/2445763
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.5'

    // Flipper https://fbflipper.com/docs/getting-started/android-native
    debugImplementation 'com.facebook.flipper:flipper:0.51.0'
    debugImplementation 'com.facebook.soloader:soloader:0.9.0'
    releaseImplementation 'com.facebook.flipper:flipper-noop:0.51.0'
}