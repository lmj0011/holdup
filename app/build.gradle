apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    // get the first 8 chars of the latest git commit, and use it as the version build name
    def appBuildPrefix =  ['sh', '-c', 'git rev-parse --short=8 HEAD'].execute().text.trim()

    defaultConfig {
        applicationId "name.lmj0011.holdup"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 5
        versionName "0.1.0"
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // ref: https://stackoverflow.com/a/48674264/2445763
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled true

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources true

            debuggable false
            resValue("string", "app_build", appBuildPrefix)
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            shrinkResources true
            debuggable true
            resValue("string", "app_build", appBuildPrefix)
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation ("com.google.android.material:material:1.3.0-alpha04") {
        exclude(group: 'androidx.recyclerview',  module: 'recyclerview')
        exclude(group: 'androidx.recyclerview',  module: 'recyclerview-selection')
    }
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.recyclerview:recyclerview:1.2.0-beta01"
    implementation "androidx.recyclerview:recyclerview-selection:1.1.0-rc03"
    implementation "androidx.datastore:datastore-preferences:1.0.0-alpha05"
    implementation project(path: ':auth')
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Room dependencies
    final room_version = '2.2.5'
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // WorkManager
    final work_version = "2.4.0"
    implementation "androidx.work:work-runtime-ktx:$work_version"

    final coroutines_version = '1.4.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // androidx.preference
    implementation "androidx.preference:preference-ktx:1.1.1"

    // dependency injection
    implementation 'org.kodein.di:kodein-di:7.0.0'

    // An adaptation of the JSR-310 backport for Android.
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    final moshi_version = "1.11.0"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    kapt           "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    final okhttp_ver = '4.9.0'
    implementation "com.squareup.okhttp3:okhttp:$okhttp_ver"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_ver"

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    implementation 'com.kroegerama:bottomsheet-imagepicker:1.1.2'

    implementation 'com.github.javafaker:javafaker:1.0.2'

}